layout(location = 3) in uvec4 a_joints;
layout(location = 4) in vec4 a_weights;

layout(set = 2, binding = 1) uniform sampler2D skin_joints;

mat4 skin_jointMatrix(uint idx)
{
    vec4 meta = texelFetch(skin_joints, ivec2(0), 0);
    int width = textureSize(skin_joints, 0).x;

    vec4 m[3];   

    int i = int(idx) * 3 + int(meta.x) * gl_InstanceIndex + 1;
    int y = i / width;
    int x = i % width;
    for (int n = 0; n < 3; n++)
    {
        m[n] = texelFetch(skin_joints, ivec2(x, y), 0);

        if (x == width - 1)
        {
            y++;
            x = 0;
        }
        else
        {
            x++;
        }
    }

    return mat4(
        vec4(m[0].xyz, 0.0),
        vec4(m[1].xyz, 0.0),
        vec4(m[2].xyz, 0.0),
        vec4(m[0].w, m[1].w, m[2].w, 1.0));
}

void skin_transform(inout vec4 pos) {
    mat4 m = 
        skin_jointMatrix(a_joints.x) * a_weights.x + 
        skin_jointMatrix(a_joints.y) * a_weights.y + 
        skin_jointMatrix(a_joints.z) * a_weights.z +
        skin_jointMatrix(a_joints.w) * a_weights.w;
    pos = m * pos;
}

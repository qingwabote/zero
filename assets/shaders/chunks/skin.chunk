layout(location = 3) in uvec4 a_joints;
layout(location = 4) in vec4 a_weights;

layout(set = 3, binding = 0) uniform sampler2D jointMap;

void skin_transform(inout vec4 pos, float offset) {
    float width = float(textureSize(jointMap, 0).x);
    float width_inv = 1.0 / width;

    float offset_texel = offset / 4.0;

    mat4 mat = mat4(0.0);
    for (int n = 0; n < 4; n++)
    {
        float index = float(a_joints[n]) * 3.0 + offset_texel;

        vec4 c[3];
        for(int j = 0; j < 3; j++) 
        {
            float i = index + float(j);
            float y = floor(i * width_inv);
            float x = i - width * y;
            c[j] = texelFetch(jointMap, ivec2(x, y), 0);
        }
        mat += mat4(vec4(c[0].xyz, 0.0), vec4(c[1].xyz, 0.0), vec4(c[2].xyz, 0.0), vec4(c[0].w, c[1].w, c[2].w, 1.0)) * a_weights[n];
    }
    pos = mat * pos;
}

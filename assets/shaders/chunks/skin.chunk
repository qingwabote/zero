layout(location = 3) in uvec4 a_joints;
layout(location = 4) in vec4 a_weights;

layout(set = 3, binding = 0) uniform sampler2D jointMap;

mat4 skin_jointMatrix(uint offset, uint idx)
{
    uint width = uint(textureSize(jointMap, 0).x);

    vec4 m[3];   

    uint i = offset / 4u + idx * 3u;
    uint y = i / width;
    uint x = i % width;
    for (uint n = 0u; n < 3u; n++)
    {
        m[n] = texelFetch(jointMap, ivec2(x, y), 0);

        if (x == width - 1u)
        {
            y++;
            x = 0u;
        }
        else
        {
            x++;
        }
    }

    return mat4(
        vec4(m[0].xyz, 0.0),
        vec4(m[1].xyz, 0.0),
        vec4(m[2].xyz, 0.0),
        vec4(m[0].w, m[1].w, m[2].w, 1.0));
}

void skin_transform(inout vec4 pos, uint offset) {
    mat4 m = 
        skin_jointMatrix(offset, a_joints.x) * a_weights.x + 
        skin_jointMatrix(offset, a_joints.y) * a_weights.y + 
        skin_jointMatrix(offset, a_joints.z) * a_weights.z +
        skin_jointMatrix(offset, a_joints.w) * a_weights.w;
    pos = m * pos;
}
